~ estanteria_latas
do show_portrait("allen1")
Allen: (Conservas. El mejor amigo de los paranoicos apocalípticos.)
do show_portrait("tulip3")
Tulip: ¡Hay latas nuevas!
do show_portrait("allen2")
Allen: ¿De qué?
do show_portrait("tulip1")
Tulip: Mira, esta dice que es una lasaña de verduras.
do show_portrait("allen6")
Allen: …Esto es un sacrilegio y un insulto a todas las lasañas del planeta.

- Albóndigas en salsa
	do show_portrait("tulip3")
	Tulip: Uh, tienen una forma muy misteriosa.
	do show_portrait("allen2")
	Allen: El verdadero misterio es si alguna vez estuvieron cerca de una vaca.
	do show_portrait("tulip1")
	Tulip: ¡Igual están buenas!
	do show_portrait("allen10")
	Allen: O igual son goma con sabor a arrepentimiento. Lo descubriremos.
	do put_in_cart("Albóndigas en salsa")
- Sopa de champiñones
	do show_portrait("tulip6")
	Tulip: ¡Esto con pan tiene que estar buenísimo!
	do show_portrait("allen10")
	Allen: O puede saber a yeso. Es un 50/50.
	do show_portrait("tulip1")
	Tulip: Me encanta una buena apuesta.
	do put_in_cart("Sopa de champiñones")
- Chili picante
	do show_portrait("tulip12")
	Tulip: ¡Tiene pinta de que está potente!
	do show_portrait("allen7")
	Allen: ¿Potente o arma biológica?
	do show_portrait("tulip6")
	Tulip: Seremos los que lo decidan. ¡Por la ciencia!
	do put_in_cart("Chili picante")
- Nada, paso
	do show_portrait("tulip4")
	Tulip: ¡Pero las latas siempre vienen bien!
	do show_portrait("allen4")
	Allen: Prefiero no desafiar la ciencia alimentaria más de lo necesario.
	do show_portrait("tulip2")
	Tulip: ¡Tienes que aprender a vivir un poco más al límite!
	do show_portrait("allen")
	Allen: Una cosa es vivir al límite y otra es auto-administrarse microdosis de veneno.
=> END!

~ estanteria_dulces
do show_portrait("allen1")
Allen: (Chocolate, caramelos, galletas…)
do show_portrait("tulip3")
Tulip: Te dije que te compraría galletas si venías, así que elige tú el paquete.

- Las galletas normales de toda la vida.
	do show_portrait("tulip8")
	Tulip: Clásico, confiable, pero aburrido. 
	do show_portrait("allen4")
	Allen: Pues la próxima vez eliges tú.
	do put_in_cart("Caja de galletas normales")
- Las rellenas de chocolate.
	do show_portrait("tulip6")
	Tulip: Buenísimas. Intentaré no terminarlas de una sentada.
	do show_portrait("allen2")
	Allen: Pensaba que eran para mí.
	do show_portrait("tulip9")
	Tulip: ¡Compartir es vivir!
	do put_in_cart("Caja de galletas rellenas")
- Una al azar, qué más da.
	do show_portrait("tulip13")
	Tulip: ¿¿¿Al azar??? ¡Para eso las elijo yo!
	do show_portrait("tulip6")
	Tulip coge un paquete de galletas con pasas.
	do show_portrait("allen6")
	Allen: …eres un monstruo.
	do show_portrait("tulip11")
	Tulip: Los amantes de las pasas siempre seremos unos incomprendidos.
	do put_in_cart("Caja de galletas con pasas")
- No quiero galletas.
	do show_portrait("tulip4")
	Tulip mira a Allen con decepción.
	do show_portrait("allen4")
	Allen: …Si quieres galletas, no me uses de excusa.
	do show_portrait("tulip11")
	Tulip: Jooo.
=> END!

~ frigorifico_bebidas
do show_portrait("allen2")
Allen: (Para gustos, bebidas. Hay una fina línea entre hidratación natural y azúcar con sabor artificial.)
do show_portrait("tulip3")
Tulip: Me apetece beber algo. ¡Mira, leche con sabor a plátano!

- Agua
	do show_portrait("tulip13")
	Tulip: ¿En serio?
	do show_portrait("allen1")
	Allen: Es lo más sano de todo lo que hay aquí.
	do put_in_cart("Refresco de cola")
- Refresco de Cola
	do show_portrait("tulip5")
	Tulip: Tu dentista te echaría la bronca.
	do show_portrait("allen7")
	Allen: Mi dentista no tiene porqué enterarse.
	do put_in_cart("Agua")
- Leche con sabor a plátano
	do show_portrait("tulip6")
	Tulip: ¡Siiiiii! ¡Qué ganas de probarla!
	do show_portrait("allen3")
	Allen: (Hay que darle un capricho de vez en cuando.)
	do put_in_cart("Leche con sabor a plátano")
- Nada
	do show_portrait("tulip4")
	Tulip: …¿Estás bien?
	do show_portrait("allen2")
	Allen: ¿Por?
	do show_portrait("tulip4")
	Tulip: …No sé, al menos coge agua o algo.
	do show_portrait("allen10")
	Allen: …Vale.
	do put_in_cart("Agua")
=> END!

~ frigorifico_lacteos
do show_portrait("allen1")
Allen: (Queso, yogur, mantequilla, leche…)
do show_portrait("tulip3")
Tulip: ¿Sabías que el queso es leche vieja?
do show_portrait("allen8")
Allen: ¿Sabías que el yogur es leche con bacterias?
do show_portrait("tulip15")
Tulip: …Me niego a aceptar esta información.

- Leche
	do show_portrait("tulip3")
	Tulip: Una opción muy sensata.
	do show_portrait("allen11")
	Allen: Y necesaria. Me niego a volver a desayunar cereales con agua.
	do show_portrait("tulip13")
	Tulip: ¡Fue un experimento!
	do show_portrait("allen12")
	Allen: Y una experiencia traumática.
	do put_in_cart("Leche")
- Queso
	do show_portrait("tulip6")
	Tulip: Un buen queso en lonch—
	do show_portrait("allen4")
	Allen: No. Vamos a comprar el de verdad.
	do show_portrait("tulip4")
	Tulip: ¿Cómo que el de verdad?
	do show_portrait("allen9")
	Allen: Tulip, ese es de plástico. Este es de verdad.
	do put_in_cart("Queso")
- Yogures
	do show_portrait("allen8")
	Allen: ¿Entonces no quieres un poco de tu leche con bacterias con sabor a fresa?
	do show_portrait("tulip8")
	Tulip: …
	do show_portrait("allen8")
	Allen: Sabes que es tu favorito.
	do show_portrait("tulip5")
	Tulip: …Maldito seas tú y tus bacterias deliciosas. Compra 6.
	do put_in_cart("Yogures")
- No me apetece mucho, la verdad.
	do show_portrait("tulip1")
	Tulip: Creo que de todas formas tenemos de todo en casa.
	do show_portrait("allen10")
	Allen: Mejor no comprar de más, que luego sabemos qué pasa.
	do show_portrait("tulip13")
	Tulip: Eso fue UNA vez.
=> END!

~ frigorifico_congelados
do show_portrait("allen3")
Allen: (Mira, helado de menta con chocolate…)
do show_portrait("tulip14")
Tulip: Ni se te ocurra.
do show_portrait("allen5")
Allen: Pero si no he—
do show_portrait("tulip14")
Tulip: Ni se te ocurra.

- Una pizza
	do show_portrait("tulip3")
	Tulip: Vienen bien si organizamos una noche de juegos.
	do show_portrait("allen7")
	Allen: Hace tiempo que no preparamos una…
	do show_portrait("tulip6")
	Tulip: ¡Podríamos hacerlo!
	do show_portrait("allen4")
	Allen: Si viene Tino, me niego.
	do put_in_cart("Pizza congelada")
- Helado
	do show_portrait("allen4")
	Allen: Quiero mi helado de menta.
	do show_portrait("tulip4")
	Tulip: Pero sí sabe a pasta de dientes…
	do show_portrait("allen9")
	Allen: Pues entonces quiero mi helado de pasta de dientes, déjame en paz.
	do put_in_cart("Helado de menta y chocolate")
- Nuggets de Pollo
	do show_portrait("allen11")
	Allen: Están demasiado buenos para cuestionar si de verdad son pollo o no.
	do show_portrait("tulip3")
	Tulip: Totalmente de acuerdo. No debemos cuestionarlo.
	do show_portrait("allen7")
	Allen: Es mejor así.
	do put_in_cart("Nuggets de pollo congelados")
- Mejor nada
	do show_portrait("allen12")
	Allen: No hay nada que sepa peor que un descongelado olvidado al fondo del congelador.
	do show_portrait("tulip3")
	Tulip: Ahora que lo dices, creo que tenemos unos conos de chocolate del año pasado.
	do show_portrait("allen11")
	Allen: …Reliquias de tiempos olvidados…
	do show_portrait("tulip1")
	Tulip: No seas dramático.
=> END!

~ estanteria_frescos
do show_portrait("allen1")
Allen: (Carne, pescado, verduras, frutas. Todo muy rico, pero se pone malo en nada.)
do show_portrait("tulip3")
Tulip: Eh, podemos comprar verdura y así preparamos sopita.
do show_portrait("allen9")
Allen: Mmm… Sopita.

- Verduras
	do show_portrait("tulip6")
	Tulip: Con unas zanahorias además mejoraremos nuestra vista.
	do show_portrait("allen11")
	Allen: … 
	do show_portrait("tulip13")
	Tulip: …¿Qué?
	do show_portrait("allen7")
	Allen: No. Nada.
	do show_portrait("allen11")
	Allen: (Me da demasiada pena decirle que es un mito.)
	do put_in_cart("Zanahorias")
- Carne
	do show_portrait("allen3")
	Allen: El pollo en la sopa queda increíble.
	do show_portrait("tulip7")
	Tulip: Como una buena mantita en invierno.
	do show_portrait("allen9")
	Allen: …Qué hambre.
	do put_in_cart("Pechugas de pollo")
- Pescado
	do show_portrait("allen2")
	Allen: Quizás queda mejor al horno que en la sopa.
	do show_portrait("tulip4")
	Tulip: Mientras le quitemos las espinas, que siempre me las trago.
	do show_portrait("allen4")
	Allen: Si aprendieras a masticar y no a engullir todo no te pasaría.
	do put_in_cart("Pescado")
- Nada, da igual
	do show_portrait("tulip8")
	Tulip: Genial idea, sopa de aire.
	do show_portrait("allen7")
	Allen: Puedo vivir de galletas.
	do show_portrait("tulip10")
	Tulip: Y yo podría golpearte con un puerro.
=> END!

~ estanteria_limpieza
do show_portrait("allen2")
Allen: (La sección menos comestible de la tienda...Solo queda un jabón.)
do show_portrait("tulip1")
Tulip: ¡El jabón!
do show_portrait("allen12")
Allen: No más excusas para no fregar los cacharros…
do put_in_cart("Jabón")
=> END!

~ intento_salida_sin_pagar
do show_portrait("tulip13")
Tulip: ¡Allen! ¡Ladronzuelo!
do show_portrait("allen5")
Allen: ¿Qué? Si no iba a salir.
do show_portrait("tulip2")
Tulip: Hmmmmmmm.
=> END!

~ intro_eric
do show_portrait("eric2")
Eric: Anda, ¿Esta vez has venido acompañada para no llevarte la cesta llena de chucherías?
do show_portrait("tulip13")
Tulip: ¡Oye!
do show_portrait("allen8")
Allen: Alguien tiene que asegurarse de que hay comida de verdad en la nevera.
do show_portrait("tulip13")
Tulip: ¡Pero no te pongas de su parte!
do show_portrait("eric1")
Eric: ¿Queréis echarle un vistazo a las ofertas del día?
- Pan
	do show_portrait("tulip6")
	Tulip: ¡Recién horneado! Huele que alimenta.
	do show_portrait("allen5")
	Allen: …La verdad es que sí, tiene una pinta buenísima.
	do show_portrait("eric2")
	Eric: Pues una barrita a la cesta, chicos.
	do put_in_cart("Pan")
	do only_soap = false
	do has_empty_cart = false
	=> final_compra
- Huevos
	do show_portrait("allen9")
	Allen: Me encantan las células gigantes.
	do show_portrait("eric1")
	Eric: ¿Cómo?
	do show_portrait("tulip9")
	Tulip: Déjale, es un poco rarito.
	do put_in_cart("Huevos")
	do only_soap = false
	do has_empty_cart = false
	=> final_compra
- Miel
	do show_portrait("eric1")
	Eric: Es un bote de miel del jardín de Agatha. Dice que es buena para la garganta.
	do show_portrait("tulip6")
	Tulip: ¡Ooohhh! Habrá que darle las gracias.
	do show_portrait("allen6")
	Allen: …¿Cómo que las gracias? Si lo estamos comprando.
	do put_in_cart("Miel de Agatha")
	do only_soap = false
	do has_empty_cart = false
	=> final_compra
- No, gracias
	=> final_compra

~ final_compra
do show_portrait("eric1")
Eric: ¿Entonces ya tenéis todo?

if has_empty_cart
	do show_portrait("tulip1")
	Tulip: Realmente no venimos a por nada.
	do show_portrait("eric1")
	Eric: Ah, ¿Solo estáis pasando el rato?
	do show_portrait("tulip3")
	Tulip: Realmente veníamos a por Jabo-. 
	do show_portrait("tulip13")
	Tulip: ¡ALLEN! ¡EL JABÓN!
	do show_portrait("allen11")
	Allen: No hace falta que grites. No por alto que lo digas voy a tener más ganas de vaciar el fregadero cuando volvamos a casa.
	=> END
else
	- No
		=> END!
	- Sí
		if only_soap
			do show_portrait("tulip6")
			Tulip: Una compra pequeña y muy necesaria.
			do show_portrait("eric2")
			Eric: Muy bien, pues este es vuestro total.
			do fin_compra()
			=> END!
		elif no_soap
			do show_portrait("tulip3")
			Tulip: Esto de aquí, aunque siento que nos olvidamos de algo…
			do show_portrait("eric2")
			Eric: ¿Queréis repasar?
			- Sí
				=> END
			- No
				do show_portrait("allen7")
				Allen: Seguro que no era importante…
				=> finish
		else 
			=> finish

~ finish
do show_portrait("tulip6")
Tulip: ¡Tenemos todo!
do show_portrait("eric2")
Eric: Perfecto. Buenas selecciones. Este es vuestro total.
do show_portrait("allen10")
Allen: Que nos vas a decir tú, que trabajas aquí.
do fin_compra()
=> END!

~ we_gotta_buy
do show_portrait("allen1")
Allen: (No, hay que comprar...)
=> END!